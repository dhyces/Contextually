plugins {
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

archivesBaseName = "${mod_name}-Forge-${minecraft_version}-${version}"

minecraft {
    mappings channel: "${mapping_id}", version: "${mapping_version}"

    if (project.hasProperty('forge_ats_enabled') && project.findProperty('forge_ats_enabled').toBoolean()) {
        accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
        project.logger.debug('Forge Access Transformers are enabled for this project.')
    }

    runs {
        configureEach {
            workingDirectory "$rootDir/Forge/run"
            ideaModule "${rootProject.name}.Forge.${project.name}.main"
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                contextually {
                    source project(":Common").sourceSets.main
                    source project(":Forge").sourceSets.main
                }
                testmod {
                    source sourceSets.main
                }
            }
        }

        client {
            taskName 'Client'
        }

        server {
            taskName 'Server'
        }

        data {
            args '--mod', 'testmod', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            taskName 'Data'
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    compileOnly project(":Common")
    compileOnly project(":Forge")
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
    source(project(":Forge").sourceSets.main.allSource)
}

processResources {
    from project(":Common").sourceSets.main.resources
    from project(":Forge").sourceSets.main.resources

    filesMatching("/META-INF/mods.toml") {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
